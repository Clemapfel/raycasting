#ifdef PIXEL

vec3 random_3d(in vec3 p) {
    return fract(sin(vec3(
    dot(p, vec3(127.1, 311.7, 74.7)),
    dot(p, vec3(269.5, 183.3, 246.1)),
    dot(p, vec3(113.5, 271.9, 124.6)))
    ) * 43758.5453123);
}

float gradient_noise(vec3 p) {
    vec3 i = floor(p);
    vec3 v = fract(p);

    vec3 u = v * v * v * (v *(v * 6.0 - 15.0) + 10.0);

    return mix( mix( mix( dot( -1 + 2 * random_3d(i + vec3(0.0,0.0,0.0)), v - vec3(0.0,0.0,0.0)),
    dot( -1 + 2 * random_3d(i + vec3(1.0,0.0,0.0)), v - vec3(1.0,0.0,0.0)), u.x),
    mix( dot( -1 + 2 * random_3d(i + vec3(0.0,1.0,0.0)), v - vec3(0.0,1.0,0.0)),
    dot( -1 + 2 * random_3d(i + vec3(1.0,1.0,0.0)), v - vec3(1.0,1.0,0.0)), u.x), u.y),
    mix( mix( dot( -1 + 2 * random_3d(i + vec3(0.0,0.0,1.0)), v - vec3(0.0,0.0,1.0)),
    dot( -1 + 2 * random_3d(i + vec3(1.0,0.0,1.0)), v - vec3(1.0,0.0,1.0)), u.x),
    mix( dot( -1 + 2 * random_3d(i + vec3(0.0,1.0,1.0)), v - vec3(0.0,1.0,1.0)),
    dot( -1 + 2 * random_3d(i + vec3(1.0,1.0,1.0)), v - vec3(1.0,1.0,1.0)), u.x), u.y), u.z );
}

vec3 lch_to_rgb(vec3 lch) {
    float L = lch.x * 100.0;
    float C = lch.y * 100.0;
    float H = lch.z * 360.0;

    float a = cos(radians(H)) * C;
    float b = sin(radians(H)) * C;

    float Y = (L + 16.0) / 116.0;
    float X = a / 500.0 + Y;
    float Z = Y - b / 200.0;

    X = 0.95047 * ((X * X * X > 0.008856) ? X * X * X : (X - 16.0 / 116.0) / 7.787);
    Y = 1.00000 * ((Y * Y * Y > 0.008856) ? Y * Y * Y : (Y - 16.0 / 116.0) / 7.787);
    Z = 1.08883 * ((Z * Z * Z > 0.008856) ? Z * Z * Z : (Z - 16.0 / 116.0) / 7.787);

    float R = X *  3.2406 + Y * -1.5372 + Z * -0.4986;
    float G = X * -0.9689 + Y *  1.8758 + Z *  0.0415;
    float B = X *  0.0557 + Y * -0.2040 + Z *  1.0570;

    R = (R > 0.0031308) ? 1.055 * pow(R, 1.0 / 2.4) - 0.055 : 12.92 * R;
    G = (G > 0.0031308) ? 1.055 * pow(G, 1.0 / 2.4) - 0.055 : 12.92 * G;
    B = (B > 0.0031308) ? 1.055 * pow(B, 1.0 / 2.4) - 0.055 : 12.92 * B;

    return vec3(clamp(R, 0.0, 1.0), clamp(G, 0.0, 1.0), clamp(B, 0.0, 1.0));
}

uniform float elapsed;
uniform vec2 camera_offset;
uniform float camera_scale = 1;

vec2 to_uv(vec2 frag_position) {
    vec2 uv = frag_position;
    vec2 origin = vec2(love_ScreenSize.xy / 2);
    uv -= origin;
    uv /= camera_scale;
    uv += origin;
    uv -= camera_offset;
    uv.x *= love_ScreenSize.x / love_ScreenSize.y;
    uv /= love_ScreenSize.xy;
    return uv;
}

float smooth_max(float a, float b, float k) {
    float h = clamp(0.5 + 0.5 * (b - a) / k, 0.0, 1.0);
    return mix(a, b, h) + k * h * (1.0 - h);
}

float merge(float x, float y) {
    return x - y;
    //eturn max(x, y);
}

vec4 effect(vec4 vertex_color, Image image, vec2 texture_position, vec2 frag_position) {
    vec2 uv = to_uv(frag_position);
    float aspect_ratio = love_ScreenSize.x / love_ScreenSize.y;
    vec2 pixel_size = 1 / love_ScreenSize.xy;

    vec2 line_thickness = pixel_size * 30;

    float tile_size = 32.0;
    float scale = love_ScreenSize.x / tile_size;
    uv = uv * scale;
    uv -= line_thickness;
    uv = fract(uv);

    float edge_x = smoothstep(line_thickness.x, line_thickness.x + pixel_size.x, uv.x) *
    smoothstep(1.0 - line_thickness.x, 1.0 - line_thickness.x - pixel_size.x, uv.x);
    float edge_y = smoothstep(line_thickness.y, line_thickness.y + pixel_size.y, uv.y) *
    smoothstep(1.0 - line_thickness.y, 1.0 - line_thickness.y - pixel_size.y, uv.y);
    float value = edge_x * edge_y;

    vec2 noise_uv = to_uv(frag_position) * 1 / 3;
    const int n_octaves = 1;
    float noise_value = 0.0;
    float amplitude = 2.0;
    float frequency = 1.0;
    float persistence = 0.5;

    for (int i = 0; i < n_octaves; ++i) {
        noise_value += amplitude * gradient_noise(vec3(noise_uv * frequency , i * amplitude * elapsed / 4));
        frequency *= 2;
        amplitude *= persistence;
    }

    vec3 rainbow = lch_to_rgb(vec3(0.8, 1, noise_value));
    return vec4(vec3(mix(rainbow * 0.3, vec3(0.1), value)), 1);
}

#endif